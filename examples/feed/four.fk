--- start of standard stuff ---
map : {{X -> Y} -> List X -> List Y}
map f []        = []
map f (x :: xs) = f x :: map f xs

print : {String -> [Console]Unit}
print s = map ouch s; unit

-- lazy conditional
if : {Bool -> {X} -> {X} -> X}
if true  t f = t!
if false t f = f!

and : {Bool -> Bool -> Bool}
and true  y = y
and false _ = false

not : Bool -> Bool
not true = false
not false = true

take : {Int -> List X -> List X}
take 0 _ = nil
take k (cons x xs) = cons x (take (k - 1) xs)

drop : {Int -> List X -> List X}
drop 0 xs = xs
drop k nil = nil
drop k (cons x xs) = drop (k - 1) xs
--- end of standard stuff ---

--- Interfaces

interface Next = next : Unit

interface Idle = idle : Unit

interface IdleServer = idleServer : ServerAction

data ServerAction = noAction | dataReq

interface NewData = newData : List String -> Unit

interface Request = request : Unit

interface Collect = collect : Maybe (List String)

data Maybe X = nothing | just X

words : {List String}
words! = ["one", "two", "three", "four", "five", "six", "seven", "eight", "nine", "ten", "11", "12", "13", "14", "15", "16", "17", "18"]

nth : {Int -> List X -> X}
nth 0 (cons s _)  = s
nth k (cons _ xs) = nth (k - 1) xs

len : {List X -> Int}
len nil = 0
len (cons _ xs) = 1 + len xs

concat : {List X -> List X -> List X}
concat nil ys = ys
concat (cons x xs) ys = cons x (concat xs ys)

user : {[Console, Next]Unit}
user! = inch!; next!; user!

repeatCollect : {[Collect] List String}
repeatCollect! = case (collect!) { (just x) -> x
                                 | nothing -> repeatCollect!}

-- awaiting
client : Bool -> Int -> List String -> [Console, Idle, Collect, Request]Unit
client reqInProg currItem strs =
     (if (and (len (strs) - 3 < currItem) (not reqInProg))
       {print (" requesting more data"); request!; client true currItem strs}

       {if ((len (strs))  == currItem)
           {print (" trying to collect"); client false currItem (concat strs repeatCollect!)}
           {idle!; print (nth currItem strs); client reqInProg (currItem + 1) strs}
       })

server : {List String -> [NewData, IdleServer]Unit}
server ws = case (idleServer!) { noAction -> server ws
                                | dataReq -> newData (take 4 ws); server (drop 4 ws) }

schedule : {Maybe (List String) -> <Next>Unit -> <Idle, Collect, Request>Unit -> <NewData, IdleServer> Unit -> [Console]Unit}
schedule d <next -> user> <idle -> client>    <server> =
         schedule d (user unit) (client unit) server!
schedule d <user>         <request -> client> <idleServer -> server> =
         schedule d user! (client unit) (server dataReq)
schedule d <user>         <client> <newData strs -> server> =
         schedule (just strs) user! client! (server unit)

schedule nothing <user>   <collect -> client> <server> =
         schedule nothing user! (client nothing) server!
schedule (just d) <user>  <collect -> client> <server> =
         schedule nothing user! (client (just d)) server!


main : {[Console]Unit}
main! = schedule nothing user! (client false 0 (take 4 words!)) (server words!)
