--- start of standard stuff ---
data Maybe X = nothing | just X
data Pair X Y = pair X Y

reverse' : {List X -> List X -> List X}
reverse' []        ys = ys
reverse' (x :: xs) ys = reverse' xs (x :: ys)

reverse : {List X -> List X}
reverse xs = reverse' xs []

map : {{X -> Y} -> List X -> List Y}
map f []        = []
map f (x :: xs) = f x :: map f xs

print : {String -> [Console]Unit}
print s = map ouch s; unit
--- end of standard stuff ---

-- =============================================================================
-- Description
-- Little Frank library for writing concurrent programs that can synchronise via
-- promises. When a new Async process is spawned, the caller gets a promise
-- object. When the caller wants to redeem the given promise, it `awaits`
-- the promise and blocks until the spawned process is executed.
-- =============================================================================

--------------------------------------------------------------------------------
-- Queue interface and implementation
--------------------------------------------------------------------------------

interface Queue S = enqueue : S -> Unit
                  | dequeue : Maybe S

-- FIFO queue implementation using a zipper
fq : {List S -> List S -> <Queue S>X -> X}
fq front        back <enqueue x -> k> = fq front (x :: back) (k unit)
fq []           []   <dequeue -> k>   = fq [] [] (k nothing)
fq []           back <dequeue -> k>   = fq (reverse back) [] (k dequeue!)
fq (x :: front) back <dequeue -> k>   = fq front back (k (just x))
fq front        back x                = x

fifo : {<Queue S>X -> X}
fifo <m> = fq [] [] m!

--------------------------------------------------------------------------------
-- Definitions of interfaces, data types
--------------------------------------------------------------------------------

interface Co = fork : {[Co]Unit} -> Unit
             | yield : Unit

data PromiseStatus X = done X
                     | waiting (List {X -> Unit})

data Promise X = promise (Ref (PromiseStatus X))

interface Async = async X : {[Async]X} -> Promise X
                | await X : Promise X -> X

--------------------------------------------------------------------------------
-- Transform an async process into fork/yield process
--------------------------------------------------------------------------------

interface CoRef = [Co [RefState], RefState]

-- only the first attempt to fulfill a promise is observable;
-- subsequent attempts are ignored
fulfill : {Promise X [CoRef] -> X -> [CoRef]Unit}
fulfill (promise r) x = case (read r)
                          { (done _)     -> unit
                          | (waiting fs) -> map { f -> f x } (reverse fs);
                                            write r (done x) }

asyncCoRef : {Promise X [CoRef] -> <Async [CoRef]>X -> [CoRef]Unit}
asyncCoRef p v = fulfill p v
asyncCoRef p <async c -> k> =
  let q = promise (new (waiting [])) in
  fork { asyncCoRef q c! };
  asyncCoRef p (k q)
asyncCoRef p <await (promise r) -> k> =
  case (read r)
  { (done v) -> asyncCoRef p (k v)
  | (waiting fs) ->
      let f = { v -> asyncCoRef p (k v) } in
      write r (waiting (f::fs)) }

runAsync : {<Async [CoRef]>X -> [RefState]X}
runAsync <m> = let r = new (waiting []) in
               bfFifo (asyncCoRef (promise r) (<Co()> m!));

               -- For the promise to be fullfilled now depends on all
               -- handlers in sight using their continuations
               -- linearly.

               -- This does hold for all of the defined handlers, but
               -- one could define a non-standard handler for RefState
               -- which did not invoke the continuation linearly.

               case (read r) {(done x) -> x}

bfFifo : {<Co>Unit -> Unit}
bfFifo <m> = fifo (scheduleBF (<Queue()> m!))

------------------------------------------------------------------------------
-- Scheduling as in coop.fk
------------------------------------------------------------------------------

data Proc = proc {[Queue Proc]Unit}

enqProc : {[Queue Proc]Unit} -> [Queue Proc]Unit
enqProc p = enqueue (proc p)

runNext : {[Queue Proc]Unit}
runNext! = case dequeue! { (just (proc x)) -> x!
                         | nothing         -> unit }

-- defer forked processes
scheduleBF : {<Co>Unit -> [Queue Proc]Unit}
scheduleBF <yield -> k>  = enqProc {scheduleBF (k unit)};
                           runNext!
scheduleBF <fork p -> k> = enqProc {scheduleBF (<Queue()> p!)};
                           scheduleBF (k unit)
scheduleBF unit          = runNext!

------------------------------------------------------------------------------
-- Concurrent Async programs
------------------------------------------------------------------------------

-- IMPORTANT: the effect parameter of Async must be independent of the
-- ambient
calcProg : {Int -> Int -> Int -> [E | Async [Console], Console]Int}
calcProg a b c = print "M1  ";
                 let p1 = async {print "p1  "; a + b} in
                 let p2 = async {print "p2  "; b + c} in
                 print "M2  ";
                 let v2 = await p2 in
                 print "M3  ";
                 let v1 = await p1 in
                 print "M4  ";
                 v1 + v2

--------------------------------------------------------------------------------
-- Main
--------------------------------------------------------------------------------

-- Dont delete these lines, they make this program into a unit test.
-- #desc   examples/async-await.fk
-- #return M1  M2  p1  p2  M3  M4  8

main: {[RefState, Console]Int}
main! = runAsync (calcProg 1 2 3)
